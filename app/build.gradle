apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return 5
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return "NotReleased"
    }
}

def getCommit = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return "-" + stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}
android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "io.github.asutorufa.yuhaiin"
        def documentsAuthorityValue = applicationId + ".documents"

        // Now we can use ${documentsAuthority} in our Manifest
        manifestPlaceholders =
                [documentsAuthority: documentsAuthorityValue]
        // Now we can use BuildConfig.DOCUMENTS_AUTHORITY in our code
        buildConfigField "String",
                "DOCUMENTS_AUTHORITY",
                "\"${documentsAuthorityValue}\""

        minSdkVersion 21
        targetSdkVersion 32
        versionCode getVersionCode()
        versionName getVersionName() + getCommit()
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.1.1"
    }


    splits {
        abi {
            enable true

            // Resets the list of ABIs that Gradle should create APKs for to none.
            reset()

            include "x86_64", "arm64-v8a"
        }
    }

    buildFeatures {
        compose true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    namespace 'io.github.asutorufa.yuhaiin'
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.1'

    def preference_version = "1.2.0"
    // Java language implementation
    implementation "androidx.preference:preference-ktx:$preference_version"


    implementation 'com.google.android.material:material:1.6.0'

    implementation "androidx.compose.material3:material3:1.0.0-alpha12"
    implementation "androidx.compose.material3:material3-window-size-class:1.0.0-alpha12"
    implementation "androidx.activity:activity-compose:1.4.0"
    implementation "androidx.compose.ui:ui:1.1.1"
    implementation "androidx.compose.ui:ui-tooling:1.1.1"
    debugImplementation "androidx.compose.ui:ui-tooling:1.1.1"
    implementation "androidx.compose.ui:ui-tooling-preview:1.1.1"
    implementation "androidx.compose.compiler:compiler:1.1.1"

    implementation project(":yuhaiin")
    implementation project(":logcatviewer")
}
